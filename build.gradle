plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.31"
}

group = 'de.axelrindle'
version = '2.2.0'

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://jitpack.io" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    // Kotlin stdlib
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31"

    // APIs
    compileOnly "org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT"
    compileOnly "com.github.axelrindle:PocketKnife:1.3.0"
    compileOnly "me.clip:placeholderapi:2.10.3"
}

processResources {
    filter { line ->
        line.replaceAll("@VERSION@", project.version.toString())
    }
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task copyToServer(type: Copy) {
    def dir = System.getenv("PLUGINS_DIR")
    if (dir == null) return true

    from("build/libs") {
        include "${project.name}-${project.version}.jar"
    }
    into dir
}

assemble {
    finalizedBy copyToServer
}